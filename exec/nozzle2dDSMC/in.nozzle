################################################################################
# 3D plume flow with symmetry boundary conditions
################################################################################

seed               12137
dimension          3
global             gridcut 1e-3 comm/sort yes

###########
# Species #
###########
species             air.species N2

variable nden equal 1.086e19
mixture             air N2 vstream 0.0 0.0 0.0 temp 300.0 nrho 1.086e19
mixture             air N2 frac 1.0

#variable nden equal 2.173e19
#mixture             air N2 vstream 0.0 0.0 0.0 temp 300.0 nrho 2.173e19
#mixture             air N2 frac 1.0

mixture             air_in N2 vstream 0.0 0.0 -322.336 temp 252.2 nrho 1.531e23
mixture             air_in N2 frac 1.0

###########
# Collide #
###########
collide             vss all opt.vss
collide_modify      remain no

#####################
# Boundary/Geometry #
#####################
variable rnz        equal 1.0e-3
variable L          equal 40.0*${rnz}
variable Lx         equal 2.5*${L}
variable Ly         equal 2.5*${L}
variable Lz         equal ${L}

variable nx         equal 100
variable ny         equal 100
variable nz         equal 40

boundary            ro ro so
surf_collide 1      diffuse 300 1.0
bound_modify        zlo collide 1

create_box          0.0 ${Lx} 0.0 ${Ly} 0.0 ${Lz}
create_grid         ${nx} ${ny} ${nz} block * * * 
balance_grid        rcb part

region cyl          cylinder z 0.0 0.0 ${rnz} 0 INF side in
region ocyl         cylinder z 0.0 0.0 ${rnz} 0 INF side out

fix background      emit/face air xhi yhi twopass
fix inflow          emit/face air_in zhi region cyl twopass
fix zbackground     emit/face air zhi region ocyl twopass

global nrho ${nden}
global fnum 1.0e8
global temp 300.0

create_particles air n 0

#############
# Transient #
#############
compute             1 grid all all nrho
compute             2 thermal/grid all all temp
fix                 2 ave/grid all 1 100 100 c_1[*] c_2[*] ave one
compute             1b lambda/grid f_2[1] NULL N2 kall

variable L1plume    equal ${Lx}*0.3
region gadapt       cylinder z 0.0 0.0 ${L1plume} -INF INF
group               1 grid region gadapt center

fix                 10 adapt 100 all refine coarsen value c_1b[2] 0.25 2.0 &
                    combine min thresh less more maxlevel 6 region gadapt all

stats               50
stats_style         step np nattempt ncoll maxlevel cpu

timestep            1.0e-7
run                 8000

##########
# Steady #
##########
uncompute 1
uncompute 2
uncompute 1b
unfix 2
unfix 10

reset_timestep 0

compute             1 grid all all n nrho u v w
compute             2 thermal/grid all all temp press
compute             3 pflux/grid all all momxy momyz momxz
compute             4 eflux/grid all all heatx heaty heatz
fix                 1 ave/grid all 10 100 1000 c_1[*] c_2[*] c_3[*] c_4[*] ave running
dump                1 grid all 10000 out_dsmc.* xc yc zc f_1[*]

run                 40000
